defmodule Zoi.Error do
  @moduledoc """
  Represents a validation error with detailed information.

  ## Fields

    - `code`: Error code
    - `issue`: A tuple with the message and a map with error variables.
    - `message`: Description of the error, formed from the issue message and variables.
    - `path`: A list representing the path to the location of the error.

  ## Errors

  All `Zoi` errors have a code that can be used to identify the type of error.
  The following error codes are defined:
    - `:invalid_type`
    - `:invalid_literal`
    - `:unrecognized_keys`
    - `:invalid_enum_value`
    - `:less_than`
    - `:greater_than`
    - `:less_than_or_equal_to`
    - `:greater_than_or_equal_to`
    - `:invalid_format`
    - `:custom`

  ## Example

  The error struct follows this format:

      %Zoi.Error{
        code: :invalid_type,
        issue: {"invalid type: expected %{expected}", %{expected: "string"}},
        message: "invalid type: expected string",
        path: [:user, :name]
      }

  The `:message` field is generated by replacing the placeholders in the `:issue` message.
  This allows for dynamic error messages that provide context about the validation failure and possibility for
  localization using `Gettext` or similar libraries.
  """

  @typedoc "The path to the location of the error."
  @type path :: [atom() | binary() | integer()]

  @typedoc """
  Error struct containing detailed information about a validation error.
  """
  @type t :: %__MODULE__{
          code: atom(),
          issue: {binary(), map()},
          message: binary(),
          path: path()
        }
  defexception [:code, :issue, :message, path: []]

  @impl true
  def exception(opts) do
    struct!(__MODULE__, opts)
  end

  @spec prepend_path(t(), path()) :: t()
  def prepend_path(%__MODULE__{} = error, path) when is_list(path) do
    %{error | path: path ++ error.path}
  end

  def message(%__MODULE__{message: message}) do
    message
  end
end

defmodule Zoi.ParseError do
  @moduledoc false
  defexception [:errors]

  @impl true
  def exception(opts) do
    struct!(__MODULE__, opts)
  end

  @impl true
  def message(%__MODULE__{errors: errors}) when is_list(errors) do
    """
    Parsing error:

    #{Zoi.prettify_errors(errors)}
    """
  end
end
